l:eval[read["../../lib.lil"]].vars
i:"\n" split -1 drop read["../inp/09"]
dir:("" split "UDLR") dict (list 0,-1),(list 0,1),(list -1,0),list 1,0
i: each x in i 
  a:"%1s %i" parse x
  (list dir[a[0]]),a[1]
end

on move H T do
  if (max l.absv[H-T])>1
    T+l.vsgn[H-T]
  else T end
end

on sim n do
  rp:n take list 0,0
  TV2:()
  k:0
  while k<count i
    dH:i[k][0]
    n:i[k][1]
    while n>0
      TV2:TV2,list last rp
      nrp:rp
      nrp[0]:nrp[0]+dH
      each i in 1 drop range nrp
        nrp[i]:move[nrp[i-1] nrp[i]]
      end
      rp:nrp
      n:n-1
    end
    TV2:TV2,list last rp
    k:k+1
  end
  TV2
end

show[count l.uniq[sim[2]]]
show[count l.uniq[sim[10]]]
